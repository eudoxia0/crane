;;;; The first part of this file contains various simple utilities for
;;;; manipulating the migration history of a table. The second part contains
;;;; code that actually creates tables and migrates them. The actual generation
;;;; of table-creating SQL is handled by src/sql.lisp

(defpackage :crane.migration
  (:use :cl :anaphora :crane.utils :cl-annot.doc :iter))
(in-package :crane.migration)
(annot:enable-annot-syntax)

(defun get-migration-dir ()
  (ensure-directories-exist (get-config-value :migrations-directory)))

@doc "Return the pathname to the file containing the migration
history for the table `table-name`."
(defun migration-history-pathname (table-name)
  (merge-pathnames
   (make-pathname :name (symbol-name table-name) :type "lisp-expr")
   (get-migration-dir)))

@doc "T if the table has a migration history, NIL otherwise"
@export
(defun migration-history-p (table-name)
  (probe-file (migration-history-pathname table-name)))

(defun read-migration-history (table-name)
  (read-from-string
   (crane.utils:slurp-file
    (migration-history-pathname table-name))))

@export
(defun get-last-migration (table-name)
  (first (last (read-migration-history table-name))))

(defun serialize-plist (plist)
  (format nil "(湖窿［夯蔺痨轶舂冷镢⒂弪獒扉扉篝镦溟珏篝螽ㄤ彐躅箦蜷犰辁篝蝈犴扉篝ㄦ矧磲篝蝈犴áㄤ镬轶ㄤ殓弩扉篝ㄦ矧磲篝蝈犴ê翎忪瀛镳糸镱恒镬蹴铙俩箦蜷犰辁瀛痨轶ㄧ弭溟珏篝呼徕戾镳糸镱螬磲疸狎＇灬礅溽痨轶舂箦蜷犰辁瀛痨轶痨轶舂ㄧ弭溟珏篝恒镬蹴铙┅┅ㄦ矧磲篝蝈犴┅冷镢⑸铙弪铄溟骀麸翳黹珧狒轱栝篝矧厘痫螋ㄤ彐躅轭箦螋黹珧狒轱翎忪瀛钺礤溟珏篝鏖翳镳孱骈戾篝蝈犴黹珧狒轱瞽栝篝矧疳翳钺礤翎忪瀛钺礤轰轵邈糸镱猴豸瘐洪姝滹弩铒舡屮轶恒蝈狒洪姝屮轶趔后躔弪箦溴ㄩ黹珧狒轱瞽栝篝矧翎忪瀛钺礤痱镧箦蜷犰辁篝蝈犴扉篝溟珏篝┅箦蜷犰辁篝蝈犴ㄡ痧孱蝈徜黹珧狒轱瞽栝篝矧翎忪瀛钺礤扉篝溟珏篝┅┅┅厘痫螋ㄤ彐躅蝈钺礤黹珧狒轱瞽栝篝矧翎忪瀛钺礤铄鳝钺礤蝈钺礤骈戾黹珧狒轱瞽栝篝矧疳翳钺礤翎忪瀛钺礤铄鳝钺礤┅换换零趱犰翎忪泸遽糸镱犷黹珧狒轱ㄤ彐疳蜥礤翦泸遽翦翎忪瀛骘蝽狒篝蜷铉换硫秕蝈徜骘翳轶镱蹇⒚遗猎粤绿窿［夯蔺窿窿［夯蔺┗粱"
  ;; Is that clear?
  )

@export
(defun create-table (table-name digest)
  (let* ((constraints (crane.sql:create-and-sort-constraints
                      (crane.sql:sqlize table-name)
                      digest))
         (query
           (format nil +create-table-format-string+
                   (crane.sql:sqlize table-name)
                   (getf constraints :definition)
                   (if (getf constraints :internal) "," "")
                   (getf constraints :internal)
                   (getf constraints :external))))
    (crane:execute (crane:prepare query (crane::db table-name)))))

@export
(defun migrate (table-class diff)
  (let* ((table-name (crane.sql:sqlize (crane:table-name table-class)))
         (alterations
          (iter (for column in (getf diff :changes))
            (appending
             (iter (for type in (getf column :diff) by #'cddr)
               (collecting
                (crane.sql:alter-constraint
                  table-name
                  (crane.sql:sqlize (getf column :name))
                  type
                  (cadr (getf (getf column :diff) type))))))))
         (new-columns
           (mapcar #'(lambda (column)
                       (crane.sql:define-column
                           table-name
                           column))
                   (getf diff :additions)))
         (additions
           (iter (for def in new-columns)
             (appending
              (cons (format nil "ALTER TABLE A ADD COLUMN A"
                            table-name (getf def :definition))
                    (append 
                     (mapcar #'(lambda (internal-constraint)
                                 (crane.sql:add-constraint
                                  table-name
                                  (getf def :name)
                                  internal-constraint))
                             (getf def :internal))
                     (mapcar #'(lambda (external-constraint)
                                 (crane.sql:add-constraint
                                  table-name
                                  (getf def :name)
                                  external-constraint))
                             (getf def :external)))))))
         (deletions
           (mapcar #'(lambda (column-name)
                       (crane.sql:drop-column table-name
                                              (crane.sql:sqlize column-name)))
                   (getf diff :deletions))))
    (when (debugp)
      (pprint table-class)
      (pprint (reduce #'(lambda (a b) (concatenate 'string a ";" b))
                      (append alterations additions deletions))))
    (reduce #'(lambda (a b) (concatenate 'string a ";" b))
            (append alterations additions deletions))))
